with import <nixpkgs/lib>;
with builtins;

let
  # Colors generated by going to https://colordesigner.io/gradient-generator and picking.
  background = "#1c1c1c";
  foreground = "#ffffff";
  dimForeground = "#aaa";
  highlight = "#004799";
  fadeColors =
    [ background "#004799" "#1e3b8c" "#2b2f7f" "#322270" "#361362" "#380053" ];
  gradientOpts = {
    name = "r";
    colors = fadeColors;
    separator = " ";
  };

  gradient = (import ./polybar-gradient.nix) gradientOpts [
    null
    (bg: {
      type = "internal/cpu";
      interval = 2;
      format-prefix = " CPU ";
      format-background = bg;
      format-prefix-foreground = dimForeground;
      label = "%percentage:2%%";
    })
    null
    (bg: {
      type = "internal/memory";
      interval = 2;
      format-prefix = " MEM ";
      format-background = bg;
      format-prefix-foreground = dimForeground;
      label = "%percentage_used%%";
    })
    null
    (bg: {
      type = "custom/script";
      exec = "~/bin/polybar-battery.sh";
      interval = 5;
      format-background = bg;
    })
    null
    (bg: {
      type = "internal/network";
      interface = "wlp3s0";
      interval = "3.0";

      label-connected =
        " %{F${dimForeground}}UP%{F-} %upspeed% %{F${dimForeground}}DN%{F-} %downspeed%";
      label-connected-background = bg;

      label-disconnected = " NAK";
      label-disconnected-background = bg;
    })
    null
    (bg: {
      type = "internal/date";
      interval = 3;

      date = "%m/%d";
      time = "%H:%M";
      label = "%date% %time%";

      format-prefix = "%{T3}%{T-} ";
      format-prefix-foreground = dimForeground;

      format-background = bg;
    })
    null
    (bg: {
      type = "custom/text";
      content = " V@S ";
      content-background = bg;
    })
  ];

  i3Icons = icons:
    listToAttrs (imap0 (i: icon: {
      name = "ws-icon-${toString i}";
      value = "${toString (i + 1)};${icon}";
    }) icons);

in {
  "bar/bar" = {
    width = "100%";
    height = "24";
    fixed-center = true;

    background = background;
    foreground = foreground;

    modules-left = "i3";
    modules-center = "cmus";

    modules-right = gradient.names;

    # General text font.
    font-0 = "Ttyp0:pixelsize=16;3";
    font-1 = "unifont:size=12;3";
  };

  "module/cmus" = rec {
    type = "custom/script";
    exec = toString ./polybar-cmus.sh;
    exec-if = "pgrep -x cmus";
    interval = 1;

    format = "<label>";
    format-prefix = " ";
    format-prefix-foreground = "#00a2d3";
    format-suffix = " ";
    format-suffix-foreground = format-prefix-foreground;
    label = "%output%";
    label-maxlen = 75;
  };

  "module/i3" = {
    type = "internal/i3";
    format = "<label-state> <label-mode>";
    index-sort = true;

    label-mode-padding = 2;

    label-focused = "%icon%";
    label-focused-foreground = "#ffffff";
    label-focused-background = highlight;
    label-focused-padding = 1;

    label-unfocused = "%icon%";
    label-unfocused-foreground = "#60ffffff";
    label-unfocused-padding = 1;

    label-urgent = "%icon%";
    label-urgent-foreground = "#ffffff";
    label-urgent-background = "#990047";
    label-urgent-padding = 1;
  } // (i3Icons [
    "WEB"
    "MSG"
    "VIM"
    "TRM"
    "GBF"
    "MUS"
    "-7-"
    "-8-"
    "-9-"
  ]);
} // gradient.modules
